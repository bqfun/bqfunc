config {
  type: "operations",
  schema: "holidays_in_japan",
  tags: ["asia-northeast1"],
  hasOutput: true,
}

CREATE OR REPLACE FUNCTION ${self()}(d DATE)
  AS (
    "1985-12-27" <= d
    AND EXTRACT(DAYOFWEEK FROM d) <> 1
    AND (
      ${ref("holidays_in_japan", "is_new_years_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_coming_of_age_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_national_foundation_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_emperors_birthday")}(d - 1)
      OR ${ref("holidays_in_japan", "is_vernal_equinox_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_showa_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_constitution_memorial_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_greenery_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_childrens_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_marine_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_mountain_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_respect_for_the_aged_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_autumnal_equinox_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_health_sports_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_sports_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_culture_day")}(d - 1)
      OR ${ref("holidays_in_japan", "is_labour_thanksgiving_day")}(d - 1)
      OR ${ref("holidays_in_japan", "extra_holiday_name_if_holiday")}(d - 1) IS NOT NULL
    )
    AND (
      ${ref("holidays_in_japan", "is_new_years_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_coming_of_age_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_national_foundation_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_emperors_birthday")}(d + 1)
      OR ${ref("holidays_in_japan", "is_vernal_equinox_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_showa_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_constitution_memorial_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_greenery_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_childrens_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_marine_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_mountain_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_respect_for_the_aged_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_autumnal_equinox_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_health_sports_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_sports_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_culture_day")}(d + 1)
      OR ${ref("holidays_in_japan", "is_labour_thanksgiving_day")}(d + 1)
      OR ${ref("holidays_in_japan", "extra_holiday_name_if_holiday")}(d + 1) IS NOT NULL
    )
  )
  OPTIONS (
    description = "前日と翌日の両方を「国民の祝日」に挟まれた平日は休日とする"
  )
