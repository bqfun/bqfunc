config {
  type: "operations",
  schema: "holidays_in_japan__us",
  tags: ["US"],
  hasOutput: true,
}

CREATE OR REPLACE FUNCTION ${self()}(d DATE)
  AS (
    d = "2020-07-23"
    OR d = "2021-07-22"
    OR d BETWEEN "1996-01-01" AND "2002-12-31" AND EXTRACT(MONTH FROM d) = 7 AND EXTRACT(DAY FROM d) = 20
    OR "2003-01-01" <= d AND EXTRACT(YEAR FROM d) NOT IN (2020, 2021) AND EXTRACT(MONTH FROM d) = 7 AND EXTRACT(DAYOFWEEK FROM d) = 2 AND EXTRACT(DAY FROM d) BETWEEN 15 AND 21
  )
  OPTIONS (
    description = "海の日 7月の第3月曜日 海の恩恵に感謝するとともに、海洋国日本の繁栄を願う。"
  )
