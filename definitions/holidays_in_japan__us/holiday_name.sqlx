config {
  type: "operations",
  schema: "holidays_in_japan__us",
  hasOutput: true,
}

CREATE OR REPLACE FUNCTION ${self()}(d DATE)
  AS (
    IFNULL(
      ${ref("holidays_in_japan__us", "extra_holiday_name_if_holiday")}(d),
      CASE
        WHEN ${ref("holidays_in_japan__us", "is_new_years_day")}(d) THEN "元日"
        WHEN ${ref("holidays_in_japan__us", "is_coming_of_age_day")}(d) THEN "成人の日"
        WHEN ${ref("holidays_in_japan__us", "is_national_foundation_day")}(d) THEN "建国記念の日"
        WHEN ${ref("holidays_in_japan__us", "is_emperors_birthday")}(d) THEN "天皇誕生日"
        WHEN ${ref("holidays_in_japan__us", "is_vernal_equinox_day")}(d) THEN "春分の日"
        WHEN ${ref("holidays_in_japan__us", "is_showa_day")}(d) THEN "昭和の日"
        WHEN ${ref("holidays_in_japan__us", "is_constitution_memorial_day")}(d) THEN "憲法記念日"
        WHEN ${ref("holidays_in_japan__us", "is_greenery_day")}(d) THEN "みどりの日"
        WHEN ${ref("holidays_in_japan__us", "is_childrens_day")}(d) THEN "こどもの日"
        WHEN ${ref("holidays_in_japan__us", "is_marine_day")}(d) THEN "海の日"
        WHEN ${ref("holidays_in_japan__us", "is_mountain_day")}(d) THEN "山の日"
        WHEN ${ref("holidays_in_japan__us", "is_respect_for_the_aged_day")}(d) THEN "敬老の日"
        WHEN ${ref("holidays_in_japan__us", "is_autumnal_equinox_day")}(d) THEN "秋分の日"
        WHEN ${ref("holidays_in_japan__us", "is_health_sports_day")}(d) THEN IF(d = "2019-10-14", "体育の日（スポーツの日）", "体育の日")
        WHEN ${ref("holidays_in_japan__us", "is_sports_day")}(d) THEN "スポーツの日"
        WHEN ${ref("holidays_in_japan__us", "is_culture_day")}(d) THEN "文化の日"
        WHEN ${ref("holidays_in_japan__us", "is_labour_thanksgiving_day")}(d) THEN "勤労感謝の日"
        WHEN ${ref("holidays_in_japan__us", "is_kokumin_no_kyujitsu")}(d) THEN "休日"
        WHEN ${ref("holidays_in_japan__us", "is_furikae_kyujitsu")}(d) THEN "休日"
      END
    )
  )
  OPTIONS (
    description = "国民の祝日または休日なら名前を返す、そうでない場合は NULL を返す"
  )
