config {
  type: "operations",
  schema: "holidays_in_japan__us",
  tags: ["US"],
  hasOutput: true,
}

CREATE OR REPLACE FUNCTION ${self()}(d DATE)
  AS (
    EXTRACT(MONTH FROM d) = 9 AND EXTRACT(DAY FROM d) = FLOOR(
      CASE
        WHEN EXTRACT(YEAR FROM d) BETWEEN 1851 AND 1899 THEN 22.2588
        WHEN EXTRACT(YEAR FROM d) BETWEEN 1900 AND 1979 THEN 23.2588
        WHEN EXTRACT(YEAR FROM d) BETWEEN 1980 AND 2099 THEN 23.2488
        WHEN EXTRACT(YEAR FROM d) BETWEEN 2100 AND 2150 THEN 24.2488
        ELSE - 1000000000
      END
      + 0.242194 * (EXTRACT(YEAR FROM d) - 1980)
      - FLOOR((EXTRACT(YEAR FROM d) - 1980) / 4)
    )
  )
  OPTIONS (
    description = "秋分の日 秋分日 祖先をうやまい、なくなった人々をしのぶ。"
  )
